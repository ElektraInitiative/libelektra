#Svn stuff
# $Id: configure.in 40 2004-11-26 23:25:09Z rayman $
# $LastChangedBy: rayman $

#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([libnss_elektra], [0.5], [rayman@skumler.dk])
AC_CONFIG_SRCDIR([src/lib.c])
AM_CONFIG_HEADER([src/config.h])

AC_PREFIX_DEFAULT(/usr)
AM_INIT_AUTOMAKE([nss-elektra],[0.5])

AC_CANONICAL_HOST
slibdir=NONE
case "$host" in
*linux* | *Linux*)
  # The Linux filesystem standard prescribed where to place "essential"
  # files.  I.e., when the installation prefix is "/usr" we have to place
  # shared library objects on the root partition in /lib.
  if test "$prefix" = "/usr" -o "$prefix" = "NONE"; then
    # 64bit libraries on sparc go to /lib64 and not /lib
    if test "$host_cpu" = "sparc64"; then
      slibdir="/lib64"
      if test "$libdir" = '${exec_prefix}/lib'; then
        libdir='${exec_prefix}/lib64';
      fi
    else
      slibdir="/lib"
    fi
  fi
  ;;
esac
if test "$slibdir" = "NONE"; then
  slibdir="/lib"
fi

# Checks for programs.
CFLAGS="${CFLAGS}"
LDFLAGS="${LDFLAGS}"

AC_PROG_CC
AC_PROG_INSTALL
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h syslog.h unistd.h])
AC_CHECK_HEADERS([nsswitch.h shadow.h pwd.h])

AC_CHECK_HEADER(kdb.h,,AC_MSG_ERROR(Elektra headers not available))

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_TYPE_UID_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_CHECK_FUNCS([strdup strtol])

#debug disabled by default
AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug],[Enable debug support]),
  [case "${enable_debug}" in
    yes) use_debug=1 
	 debug_parms="-g3 -ggdb -DDEBUG" ;;
    no) use_debug=0 
 	 debug_parms="-O2";;
    *) AC_MSG_ERROR(bad value ${enable_debug} for enable debug option) ;;
   esac],
  [use_debug=0])

AC_DEFINE_UNQUOTED(DEBUG, ${use_debug}, [ set to 1 if you want that some debugging output to be send to the auth log ])

AC_ARG_WITH(elektra-include, dnl
  --with-elektra-include=DIR  find Elektra (kdb) headers in DIR,
            [dnl
case "$with_elektra_include" in
''|no) ;;
*) libelektra_include="-I$withval" ;;
esac
])

# Check for a --with-elektra-lib argument.
AC_ARG_WITH(elektra-lib, dnl
  --with-elektra-lib=DIR         find Elektra (kdb) library in DIR,
            [dnl
case "$with_elektra-lib" in
''|no) ;;
*) libelektra_libs="-L$withval" ;;
esac
])

ELEKTRA_CFLAGS="$libelektra_include"
ELEKTRA_LIBS="$libelektra_libs"
LIBS="$ELEKTRA_LIBS"
# Checks for libraries.
AC_CHECK_LIB(kdb,kdbOpen,,AC_MSG_ERROR(No elektra support found))

CPPFLAGS="$CPPFLAGS $ELEKTRA_CFLAGS"
AC_SUBST(ELEKTRA_CFLAGS)
AC_SUBST(ELEKTRA_LIBS)
AC_SUBST(slibdir)
AC_SUBST(debug_parms)

AC_CONFIG_FILES([Makefile
                 src/Makefile tools/Makefile])
AC_OUTPUT

